+++
date = '2025-06-01'
draft = false
title = 'Playing around with the new vertical "tabpanel" in Vim: Simple tabpanel & "Bufferpanel"'
tags = ["Vim", "Vimscript"]
+++

In the [patch 9.1.1391](https://github.com/vim/vim/commit/be5bd4d6292fddcc103091407792730aaa48cc48), the Vim added a new UI element: [tabpanel](https://vimhelp.org/tabpage.txt.html#tabpanel).
This would have garnered a lot of attention if it was in Neovim, but unfortunately, vanilla Vimmers did not seem to be too interested in the UI change.
So I took the chance to become the first person (at least in r/vim) to configure a custom Tabpanel.

## Simple Tabpanel

I started with a simple list of tabs with the number of windows.
I found [`:h 'tabline'`](https://vimhelp.org/options.txt.html#%27tabpanel%27) to be quite helpful.
This is essentially the vertical, Vimscript version of my [Neovim tabline](blog/2025-05-13-tabline-in-lua.md).
Clicking and dragging on the tabs work fine, but using the `%X` or `%T` flag did not work.

```vim
" {{{ Simple Tabpanel
set showtabpanel=2
set fillchars+=tpl_vert:\|
set tabpanelopt=vert,align:left,columns:9
function! TabPanel() abort
  let curr = g:actual_curtabpage

  let s = printf("%2d", curr)
  let numWin = len(tabpagebuflist(curr))
  if numWin > 1
    let s .= '|  ' . numWin
  endif

  return s
endfunction
set tabpanel=%!TabPanel()
" }}}

```

![simple-tabpanel](/images/2025-06-01-tabpanel/tabpanel.jpg)

You can find the Bufferline in [this post](blog/2023-03-17-vimscript-bufferline.md) (the version in the screenshot is slightly modified).

## "Bufferpanel"

But I think it makes much more sense to use the vertical space as the list of buffers, creating the "Bufferpanel."

My first instinct was to simply replace separators in my Bufferline config with `\n`, but it was not as easy as that.
The content of `'tabline'` is evaluated *per* tab, meaning if your function returns the list of buffers, it will be printed twice if you have two tabs open.
So I created a workaround where I only display the content for the first tab and nothing for others if they exist.

```vim
" {{{ BufferPanel
set showtabpanel=2
set fillchars+=tpl_vert:\|
set tabpanelopt=vert,align:left,columns:20
function! BufferPanel() abort
  let s = '%#TabPanelFill#'

  " tabpanel is evaluated per tab; workaround to create the list only once
  if g:actual_curtabpage == 1

    " Get the list of buffers. Use bufexists() to include hidden buffers
    let bufferNums = filter(range(1, bufnr('$')), 'buflisted(v:val)')

    for i in bufferNums
      " Highlight if it's the current buffer
      let s .= (i == bufnr()) ? ('%#TabPanelSel#') : ('%#TabPanel#')  

      let s .= ' ' . i . ' '  " Append the buffer number

      " Give a [NEW] flag to an unnamed buffer
      if bufname(i) == ''
        let s .= '[NEW]'
      endif

      " Append bufname
      let bufname = fnamemodify(bufname(i), ':t')

      " Truncate bufname
      " -1 if vertical separators are on
      " -3 for the buffer number
      " -3 for the potential modified flag
      " -2 for the ..
      let lenLimit = 11
      if len(bufname) > lenLimit
        " expr-[:] is range-inclusive (i.e., [0:10] returns 11 char)
        let bufname = bufname[0:lenLimit - 1] . '..'
      endif

      let s .= bufname

      " Add modified & read only flag
      if getbufvar(i, "&modified")
        let s .= '[+]'
      endif
      if !getbufvar(i, "&modifiable")
        let s .= '[-]'
      endif
      if getbufvar(i, "&readonly")
        let s .= '[RO]'   
      endif

      let s .= "\n"
    endfor

    let s .= "%#TabPanelFill#"
  endif

  return s
endfunction

set tabpanel=%!BufferPanel()
" }}}
```

![bufferpanel](/images/2025-06-01-tabpanel/bufferpanel.jpg)

That looks quite slick, doesn't it?

You probably want to add the following `autocmd` to force redraw on buffer changes.

```vim
" Force redraw on buffer changes
autocmd BufAdd,BufCreate,BufDelete,BufWipeout * redrawtabpanel
```

Note that mouse clicking is completely broken with this.
I am not even sure if there is a way to fix it given that components like `%T` and `%X` do not seem to work.

I am also a bit disappointed that you cannot interact with the panel using the keyboard.
I would love to be able to use it as a Netrw but for buffers.
Nonetheless, we can at least make it toggle-able with the following keymap.

```vim
" Toggle bufferpanel
nnoremap <expr><silent> <leader>b &showtabpanel==2 ?
                        \ ':set showtabpanel=0<CR>' : ':set showtabpanel=2<CR>'
```

I also had to create the horizontal version of the simple Tabpanel to go with the Bufferpanel.

``` vim
" {{{ Simple Tabline
fun! SpawnTabline()
  let s = ' Tabs :) '

  for i in range(1, tabpagenr('$'))  " Loop through the number of tabs
    " Highlight the current tab
    let s .= (i == tabpagenr()) ? ('%#TabLineSel#') : ('%#TabLine#')
    let s .= '%' . i . 'T '  " set the tab page number (for mouse clicks)
    let s .= i               " set page number string

    " Add a number of window if applicable
    let numWin = len(tabpagebuflist(i))
    if numWin > 1
      let s .= '[ ' . numWin . ']'
    endif

    let s .= ' '
  endfor
  let s .= '%#TabLineFill#%T'  " Reset highlight

  " Close button on the right if there are multiple tabs
  if tabpagenr('$') > 1
    let s .= '%=%#TabLineSel#%999X[X]'
  endif
  return s
endfun

set tabline=%!SpawnTabline()  " Assign the tabline
" }}}
```

## Bug in 9.1.1400

Unfortunately, there is a bug in 9.1.1400 that for me makes `tabpanel` unusable.
Essentially, when `tabpanel` is on and set to occupy, say 20 columns, then the character in the 20th column from the opposite side gets rendered incorrectly.
I created the [issue](https://github.com/vim/vim/issues/17423) on GitHub (also include the video demonstration), fingers crossed it gets resolved soon.

## Conclusion

Can't wait for Neovim to implement this so that we can have an influx of crazy Lua plugins abusing this feature.


